// Separators
sep = comma | whitespace ;
alphaSep = div | sep ;

// Color value tokens (percentage, number, none)
percentage = integer << "%" ;
colorPercentage = percentage ;
colorValue = colorPercentage | number ;

// Color function names: rgb | hsl | hsv | hwb | lab | lch | oklab | oklch | xyz
colorType = "oklab" | "oklch" | "rgb" | "hsl" | "hsv" | "hwb" | "lab" | "lch" | "xyz" ;

// Standard color function: type[a]?(c1 sep c2 sep c3 [alphaSep alpha])
colorFunction =
    (colorType << "a"?)
    << "(",
        colorValue << sep,
        colorValue << sep,
        colorValue,
        (alphaSep >> colorValue)?
    << ")" ;

// Hex color: #RGB, #RRGGBB, #RGBA, #RRGGBBAA
hexDigits =
    (digit, digit, digit, (digit, digit, digit)?)
  | (digit, digit, digit, digit, (digit, digit, digit, digit)?) ;

hex = "#" >> hexDigits ;

// color() function spaces
colorSpace = "srgb-linear" | "srgb" | "display-p3" | "a98-rgb"
           | "prophoto-rgb" | "rec2020" | "xyz-d65" | "xyz-d50" | "xyz" ;

colorFn = "color" << "(",
    colorSpace , colorValue , colorValue , colorValue , ("/" >> colorValue)?
    << ")" ;

// color-mix() function
hueMethod = ("shorter" | "longer" | "increasing" | "decreasing") , "hue" ;
mixSpace = "srgb-linear" | "srgb" | "display-p3" | "a98-rgb" | "prophoto-rgb" | "rec2020"
         | "oklab" | "oklch" | "lab" | "lch" | "hsl" | "hwb" | "xyz-d65" | "xyz-d50" | "xyz" ;

colorMix = "color-mix" << "(",
    "in" , mixSpace , hueMethod? , ",",
    color , percentage? , ",",
    color , percentage?
    << ")" ;

// Top-level color
color = colorMix | colorFn | hex | colorFunction ;
