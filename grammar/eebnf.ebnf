letter = 
      "A" | "B" | "C" | "D" | "E" | "F" | "G"
    | "H" | "I" | "J" | "K" | "L" | "M" | "N"
    | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
    | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
    | "c" | "d" | "e" | "f" | "g" | "h" | "i"
    | "j" | "k" | "l" | "m" | "n" | "o" | "p"
    | "q" | "r" | "s" | "t" | "u" | "v" | "w"
    | "x" | "y" | "z" ;

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
        | "'" | '"' | "=" | "|" | "." | "," | ";" | "+" 
        | "-" | "*" | "/" | "?" | "%" | "&" | "^" | "~"
        | "`" | "!" | "@" | "#" | "$" | ":" | "\" | "ε"
        | "\t" | "\r" | "?w" | "??" | "//" ;

pipe = "|" ;
comma = "," ;
star = "*" ;
plus = "+" ;
question = "?" ;
minus = "-" ;
div = "/" ;
left_shift = "<<" ;
right_shift = ">>" ;
eof = "$" ;

optional_whitespace = "?w" ;
colasece = "??" ;

character =  letter | digit | symbol | "_" ;
identifier = letter , { letter | digit | "_" | "-" } ;

terminal = "'" , ( character - "'" ) * , "'" 
         | '"' , ( character - '"' ) * , '"' ;


big_comment = "/*" , /.*$/ , "*/" ;
comment = "//" , /.*$/ ;

regex = div , (character - div) * , div ;

lhs = identifier ;

term = "ε"
     | big_comment
     | identifier
     | terminal 
     | regex
     | eof
     | "(" , rhs , ")" 
     | "[" , rhs , "]"
     | "{" , rhs , "}" ;

factor = term , optional_whitespace
       | term , colasece , term
       | term , question
       | term , star
       | term , plus
       | term , minus , term
       | term , left_shift , term
       | term , right_shift , term
       | term ;

concatenation = factor , { comma , factor };
alternation = concatenation , { pipe , concatenation } ;

rhs = comment
    | alternation
    | concatenation 
    | factor ;

rule = lhs, "=", rhs, ";" ;
grammar = ( comment | rule ) * ;